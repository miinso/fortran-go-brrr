load("@rules_python//python:defs.bzl", "py_test")
load("@rules_fortran//fortran:defs.bzl", "fortran_binary", "fortran_test")

# Example: Simple BLAS DGEMM test
fortran_test(
    name = "main",
    srcs = ["main.f90"],
    copts = ["-O2"],
    deps = ["@blas//:double"],
)

# Export input files for Level 2 and 3 tests
exports_files(
    glob(["data/*.in"]),
)

# Level 2 tests
[
    py_test(
        name = "%sblat2" % prefix,
        size = "medium",
        srcs = ["run.py"],
        args = [
            "$(location @blas//:%sblat2)" % prefix,
            "$(location data/%sblat2.in)" % prefix,
        ],
        data = [
            "@blas//:%sblat2" % prefix,
            "data/%sblat2.in" % prefix,
        ],
        main = "run.py",
    )
    for prefix in ["s", "d", "c", "z"]
]

# Level 3 tests
[
    py_test(
        name = "%sblat3" % prefix,
        size = "medium",
        srcs = ["run.py"],
        args = [
            "$(location @blas//:%sblat3)" % prefix,
            "$(location data/%sblat3.in)" % prefix,
        ],
        data = [
            "@blas//:%sblat3" % prefix,
            "data/%sblat3.in" % prefix,
        ],
        main = "run.py",
    )
    for prefix in ["s", "d", "c", "z"]
]
