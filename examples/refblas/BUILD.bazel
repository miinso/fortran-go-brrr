load("//:defs.bzl", "fortran_binary")

# Example: Simple BLAS DGEMM test
fortran_binary(
    name = "main",
    srcs = ["main.f90"],
    deps = ["@blas//:double"],
    copts = ["-O2"],
)

# Export input files for Level 2 and 3 tests
exports_files(glob(["data/*.in"]))

# Level 2 tests (using single generic wrapper script)
[
    sh_test(
        name = "%sblat2" % prefix,
        srcs = ["run.sh"],
        args = [
            "$(location @blas//:%sblat2)" % prefix,
            "$(location data/%sblat2.in)" % prefix,
        ],
        data = [
            "@blas//:%sblat2" % prefix,
            "data/%sblat2.in" % prefix,
        ],
        size = "medium",
    )
    for prefix in ["s", "d", "c", "z"]
]

# Level 3 tests (using single generic wrapper script)
[
    sh_test(
        name = "%sblat3" % prefix,
        srcs = ["run.sh"],
        args = [
            "$(location @blas//:%sblat3)" % prefix,
            "$(location data/%sblat3.in)" % prefix,
        ],
        data = [
            "@blas//:%sblat3" % prefix,
            "data/%sblat3.in" % prefix,
        ],
        size = "medium",
    )
    for prefix in ["s", "d", "c", "z"]
]
